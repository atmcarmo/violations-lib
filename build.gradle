apply plugin: 'java-library'

buildscript {
 repositories {
  mavenCentral()
  mavenLocal()
 }
 dependencies {
  classpath 'se.bjurr.gradle:gradle-scripts:2.+'
  classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:1.2.1'
 }
}
project.ext.buildConfig = [
  publishing: [
    relocate: [
      'com.google',
      'com.jakewharton'
    ]
  ],
  violations: [
    updateReadme: true
  ],
  staticCodeAnalysis: [
    maxViolations: 0
  ],
]
apply from: project.buildscript.classLoader.getResource('main.gradle').toURI()

apply plugin: 'jsonschema2pojo'

jsonSchema2Pojo {
  source = files("${sourceSets.main.output.resourcesDir}/json")
  targetDirectory = file("src/gen/java")
  targetPackage = 'se.bjurr.violations.lib.model.generated.sarif'
  generateBuilders = true
  annotationStyle = 'none'
  includeGeneratedAnnotation = false
  removeOldOutput = true
}

dependencies {
 api 'com.google.code.gson:gson:2.10.1'

 testImplementation 'junit:junit:4.13.2'
 testImplementation 'org.assertj:assertj-core:3.25.2'
 testImplementation 'uk.co.jemos.podam:podam:8.0.1.RELEASE'
 testImplementation 'com.approvaltests:approvaltests:22.3.3'
 testImplementation 'com.networknt:json-schema-validator:1.3.0'
}
